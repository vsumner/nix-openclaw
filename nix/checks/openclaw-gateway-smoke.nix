{
  lib,
  pkgs,
  stdenv,
  nodejs_24,
  openclawGateway,
  includeRuntimePluginSmoke ? false,
  runtimePluginSmokeId ? "diagnostics-prometheus",
}:

stdenv.mkDerivation {
  pname =
    if includeRuntimePluginSmoke then
      "openclaw-runtime-plugin-gateway-smoke"
    else
      "openclaw-gateway-smoke";
  version = lib.getVersion openclawGateway;

  dontUnpack = true;
  dontConfigure = true;
  dontBuild = true;

  nativeBuildInputs = [ nodejs_24 ];

  env = {
    OPENCLAW_GATEWAY = openclawGateway;
  }
  // lib.optionalAttrs includeRuntimePluginSmoke {
    OPENCLAW_RUNTIME_PLUGIN_SMOKE_ID = runtimePluginSmokeId;
    OPENCLAW_RUNTIME_PLUGIN_SMOKE_ROOT = "${pkgs.openclawRuntimePlugins.${runtimePluginSmokeId}}";
  };

  __darwinAllowLocalNetworking = true;

  doCheck = true;
  checkPhase = "${nodejs_24}/bin/node ${../scripts/gateway-smoke.mjs}";
  installPhase = "${../scripts/empty-install.sh}";
}
